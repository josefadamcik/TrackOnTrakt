apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.9"
    reportsDir = file("$buildDir/reports")
}

// what to exclude from coverage report
// UI, "noise", generated classes, platform classes, etc.
def excludes = [
        '**/R.class',
        '**/R$*.class',
        '**/*$ViewInjector*.*',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*.*'
]

def unitExcludes = [
        '**/*Fragment.*',
        '**/*Activity.*'

]
unitExcludes.addAll(excludes)

task jacocoUnitTestReport(type: JacocoReport, dependsOn: "testInternalDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Debug build"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    // what to exclude from coverage report
    // UI, "noise", generated classes, platform classes, etc.

    // generated classes
    classDirectories = fileTree(
            dir: "$buildDir/intermediates/classes/internal/debug",
            excludes: unitExcludes
    ) + fileTree(
            dir: "$buildDir/tmp/kotlin-classes/internalDebug",
            excludes: unitExcludes
    )

    // sources
    sourceDirectories = files([
            android.sourceSets.main.java.srcDirs
    ])
    executionData = files("$buildDir/jacoco/testInternalDebugUnitTest.exec" )
}


task jacocoSpoonTestReport(type: JacocoReport, dependsOn: "spoonInternalDebugAndroidTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Spoon Debug build"

    reports {
        xml.enabled = true
        html.enabled = true
    }


    // generated classes
    classDirectories = fileTree(
            dir: "$buildDir/intermediates/classes/internal/debug",
            excludes: excludes
    ) + fileTree(
            dir: "$buildDir/tmp/kotlin-classes/internalDebug",
            excludes: excludes
    )

    // sources
    sourceDirectories = files([
            android.sourceSets.main.java.srcDirs
    ])
    executionData = files("$buildDir/spoon-output/internalDebug/coverage/merged-coverage.ec" )
}